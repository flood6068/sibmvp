MODULE Bill;

REQUIRE Order;

@defineDocument(bill, 'Счет');

CLASS BillClass 'Вид счета' {
    purchase 'от подрядчика',
    sale 'на заказчика'
}

name 'Тип счета' (BillClass t) = staticCaption(t);

CLASS BillType 'Тип счета' {
    first '17.5%',
    second '30%',
    full '100%'
}

name 'Тип счета' (BillType t) = staticCaption(t);
rate (BillType t) = CASE 
    WHEN t == BillType.first THEN 0.175 
    WHEN t == BillType.second THEN 0.3
    ELSE 1;

type 'Тип' = DATA BillType (Bill) NONULL;
nameType 'Тип' (Bill b) = STRING[30](name(type(b))) CHARWIDTH 30;
rate (Bill b) = rate(type(b));
class 'Вид' = DATA BillClass (Bill) NONULL;
nameClass 'Вид' (Bill b) = name(class(b));

EXTEND FORM bill
    PROPERTIES(o) nameType, nameClass
;

DESIGN bill {
    BOX(o) {
        MOVE PROPERTY(nameType(o)) { notNull = TRUE; }
        MOVE PROPERTY(nameClass(o)) { notNull = TRUE; }
    }
}

EXTEND FORM bills
    PROPERTIES(o) READONLY nameType, nameClass
;

currency 'Валюта' = DATA Currency (Bill);
nameCurrency 'Валюта' (Bill b) = name(currency(b));

CLASS BillLine 'Строка счета';

bill 'Счет' = DATA Bill (BillLine);
item 'Номенклатура' = DATA Item (BillLine);
idItem 'Код' (BillLine l) = id(item(l));
nameItem 'Номенклатура' (BillLine l) = name(item(l));
quantity 'Кол-во' = DATA NUMERIC[12,5] (BillLine);
nameUom 'Ед. изм.' (BillLine l) = nameUom(item(l));
price 'Цена' = DATA NUMERIC[12,2] (BillLine);
currency 'Валюта' (BillLine l) = currency(bill(l));
nameCurrency 'Валюта' (BillLine l) = name(currency(l));
rate (BillLine l) = rate(bill(l));
sum 'Сумма' (BillLine l) = NUMERIC[10,2](quantity(l) * price(l) * rate(l));
note 'Комментарий' = DATA STRING[200] (BillLine);

order 'Заказ' = DATA Order (Bill);
orderNumber 'Заказ' (Bill b) = number(order(b));

dataSum 'Сумма' = DATA NUMERIC[12,2] (Bill);
sum 'Сумма' (Bill b) = OVERRIDE dataSum(b), GROUP SUM sum(BillLine l) IF bill(l) = b;
isPayed 'Оплачено' = DATA BOOLEAN (Bill);

EXTEND FORM bill
    PROPERTIES(o) orderNumber,currency, sum, isPayed

    OBJECTS l = BillLine
    PROPERTIES(l) idItem, nameItem, quantity, nameUom, price, nameCurrency, sum, note
    PROPERTIES(l) NEW, DELETE 
    FILTERS bill(l) = o
;

EXTEND FORM bills
    PROPERTIES(o) READONLY orderNumber, sum, isPayed
;

numeratorBill = DATA Numerator ();
nameNumeratorBill 'Нумератор счетов' = name(numeratorBill());

EXTEND FORM option
    PROPERTIES nameNumeratorBill()
;

DESIGN option {
    OBJECTS {
        MOVE PROPERTY(nameNumeratorBill());
    }
}

WHEN SET(Bill b IS Bill) AND NOT number(b) AND numeratorBill() DO {
    number(b) <- CONCAT '/', series(numeratorBill()), curStringValue(numeratorBill());
    incrementValueSession(numeratorBill());
}

createBill 'Создать счет' (Order o) {
    NEWSESSION {
        NEW b = Bill {
            IF numeratorBill() THEN {
                number(b) <- CONCAT '/', series(numeratorBill()), curStringValue(numeratorBill());
                incrementValueSession(numeratorBill());
            }
            order(b) <- o;
            date(b) <- currentDate();
            currency(b) <- currency(o);
            IF type(o) == OrderType.purchase THEN class(b) <- BillClass.purchase; 
            ELSE  class(b) <- BillClass.sale; 
            
            FOR order(OrderLine ll) = o DO NEW l = BillLine {
                bill(l) <- b;
                item(l) <- item(ll);
                quantity(l) <- quantity(ll);
                price(l) <- price(ll);
            }
            SHOW bill OBJECTS o = b;
        }
    }
}

sumPaid 'Сумма (оплачено)' (Order o) = 
    GROUP SUM sum(Bill b) IF isPayed(b) AND order(b) = o AND currency(b) = currency(o);
sumLeft 'Сумма (остаток)' (Order o) = sum(o) (-) sumPaid(o);

EXTEND FORM order
    PROPERTIES(o) sumPaid, sumLeft, createBill
;

