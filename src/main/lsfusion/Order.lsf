MODULE Order;

REQUIRE Main, Item, Currency, Numerator, Project;

@defineDocument(order, 'Заказ');

currency 'Валюта' = DATA Currency (Order) INDEXED;
nameCurrency 'Валюта' (Order o) = name(currency(o));

CLASS OrderLine 'Строка заказа';

order 'Заказ' = DATA Order (OrderLine) INDEXED;
item 'Номенклатура' = DATA Item (OrderLine) INDEXED;
nameItem 'Номенклатура' (OrderLine l) = name(item(l));
quantity 'Кол-во' = DATA NUMERIC[15,5] (OrderLine) INDEXED;
nameUom 'Ед. изм.' (OrderLine l) = nameUom(item(l));
price 'Цена' = DATA NUMERIC[12,2] (OrderLine) INDEXED;
currency 'Валюта' (OrderLine l) = currency(order(l));
nameCurrency 'Валюта' (OrderLine l) = name(currency(l));
sum 'Сумма' (OrderLine l) = NUMERIC[10,2](quantity(l) * price(l)) INDEXED;

EXTEND FORM order
    OBJECTS l = OrderLine
    PROPERTIES(l) nameItem, quantity, nameUom, price, sum, nameCurrency
    PROPERTIES(l) NEW, DELETE 
    FILTERS order(l) = o
;

sum 'Сумма' (Order o) =  GROUP SUM sum(OrderLine l) IF order(l) = o;
sumRubles 'Сумма (рубли)' (Order o) = sum(o) * convertRate(currency(o), currency('RUB'));
sumEuros 'Сумма (евро)' (Order o) = sum(o) * convertRate(currency(o), currency('EUR'));
sumYuans 'Сумма (юани)' (Order o) = sum(o) * convertRate(currency(o), currency('YUAN'));

isClosed 'Закрыт' = DATA BOOLEAN (Order) INDEXED;

project = DATA Project (Order) INDEXED;
nameProject 'Проект' (Order o) = name(project(o));

EXTEND FORM order
    PROPERTIES(o) sum, isClosed BEFORE id(o), nameCurrency, nameProject
;

EXTEND FORM orders
    PROPERTIES(o) READONLY isClosed BEFORE id(o), sumRubles, sumEuros, sumYuans, nameProject
;

CLASS OrderType 'Тип заказа' {
    purchase 'На закупку',
    sale 'На продажу'
}

name (OrderType t) = staticCaption(t);

type 'Тип' = DATA OrderType (Order) INDEXED;
nameType 'Тип' (Order o) = name(type(o));

EXTEND FORM order
    PROPERTIES(o) nameType
;

EXTEND FORM orders
    PROPERTIES(o) READONLY nameType
;

numeratorOrder = DATA Numerator ();
nameNumeratorOrder 'Нумератор заказов' = name(numeratorOrder());

EXTEND FORM option
    PROPERTIES nameNumeratorOrder()
;

DESIGN option {
    OBJECTS {
        MOVE PROPERTY(nameNumeratorOrder());
    }
}

WHEN SET(Order o IS Order) AND NOT number(o) AND numeratorOrder() DO {
    number(o) <- CONCAT '/', series(numeratorOrder()), curStringValue(numeratorOrder());
    incrementValueSession(numeratorOrder());
}

