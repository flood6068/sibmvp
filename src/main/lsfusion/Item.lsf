MODULE Item;

REQUIRE Main;

@defineObject(item, 'Номенклатура');

@defineObject(uom, 'Ед. изм.');

description 'Описание' = DATA STRING (Item) INDEXED CHARWIDTH 20;
foreignCode 'Код ТН ВЭД' = DATA STRING (Item) INDEXED;
uom 'Ед. изм.' = DATA Uom (Item) INDEXED;
nameUom 'Ед. изм.' (Item i) = name(uom(i));
weight 'Вес' = DATA NUMERIC[15,5] (Item) INDEXED;
weightUom 'Ед. изм. веса' = DATA Uom (Item) INDEXED;
nameWeightUom 'Ед. изм. веса' (Item i) = name(weightUom(i));
storage 'Хранение' = DATA BOOLEAN (Item) INDEXED;
isComponent 'Компонент' = DATA BOOLEAN (Item) INDEXED;
parent 'Компонент для' = DATA Item (Item) INDEXED;
nameParent 'Компонент для' (Item i) = name(parent(i));
article 'Артикул' = DATA STRING[30] (Item) INDEXED;
tag '№ оборудования' = DATA STRING (Item) INDEXED;
position 'Позиция' = DATA STRING (Item) INDEXED;
subposition 'Субпозиция' = DATA STRING (Item) INDEXED;

CLASS ItemType 'Тип номенклатуры' {
    material 'Материалы',
    equipment 'Оборудование',
    service 'Услуга',
    work 'Работа'
}

name (ItemType t) = staticCaption(t);

type = DATA ItemType (Item) INDEXED;
nameType 'Тип' (Item i) = name(type(i));

EXTEND FORM item
    PROPERTIES(o) description, foreignCode, nameUom, weight, nameType, storage
    PROPERTIES(o) article SHOWIF type(o) == ItemType.material
    PROPERTIES(o) tag SHOWIF type(o) == ItemType.equipment, isComponent SHOWIF type(o) == ItemType.equipment, 
        nameParent SHOWIF type(o) == ItemType.equipment
;
EXTEND FORM items
    PROPERTIES(o) READONLYIF NOT allowEdit() description, foreignCode, nameUom, weight, nameType, storage, article, tag, isComponent, nameParent
;
